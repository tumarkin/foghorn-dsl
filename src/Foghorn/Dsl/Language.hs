{- |
Module      : Foghorn.Dsl.Language
Description : High-level DSL for building econometric research projects
Copyright   : (c) Rob Tumarkin, 2025
License     : Non-commercial use only
Maintainer  : https://github.com/tumarkin
Stability   : experimental

Provides the high-level DSL for building econometric research projects. Users write
projects as monadic programs using functions like 'estimate_', 'observe_',
'comment_', etc. The project is built using Effectful effects and can be
interpreted to generate data management and econometric code.

= Usage

A typical project looks like:

@
myProject :: Project XCoficatVar (Panel DealNumber_ AnnouncementDate_) ()
myProject = do
    header_ "Main Results"

    comment_ "Load data"
    readCsv_ "deal_data"

    comment_ "Run baseline regression"
    estimate_ myRegression

    tableDisplay_ $ do
        estimate_ reg1
        estimate_ reg2
        estimate_ reg3
@

The 'Project' type is a monad that supports:

* 'estimate_' - Run an estimator (regression, summary stats, etc.)
* 'observe_' - Include variables in the final dataset in addition to those from specifications
* 'comment_' - Add explanatory comments
* 'header_' - Add section headers
* 'readCsv_' - Load CSV data
* 'raw_' - Insert raw code
* 'tableDisplay_' / 'tableSave_' - Format estimation output

-}

{-# LANGUAGE UndecidableInstances #-}
module Foghorn.Dsl.Language (
    Project,
    Stmt (..),

    -- * DSL
    blankLine_,
    comment_,
    divider_,
    estimate_,
    table_,
    tableDisplay_,
    tableSave_,
    header_,
    observe_,
    readCsv_,
    raw_,

    -- -- * Project manipulation
    -- mapProject,

    -- * Quasi-quoter
    module Foghorn.Dsl.Language.Quote,
    module Foghorn.Dsl.Language.Types,
) where

import qualified Data.Text as T
import Foghorn.Dsl.Language.Quote
import Foghorn.Dsl.Language.Types
import Foghorn.Dsl.Language.Statement (blankLine__, comment__, divider__, estimate__, table__, header__, observe__, readCsv__, raw__)
import Foghorn.Dsl.Interpreter.ModelNames (modelNames)
import Effectful
import Relude
import System.FilePath

{- | Type alias for research project programs.

A project is an Effectful effect program that uses 'Stmt' as its effect.
The type parameters are:

* @xsource@ - Variable source (e.g., 'XCoficatVar')
* @i@ - Index structure (e.g., 'Panel DealNumber_ AnnouncementDate_')
-}
type Project xsource i = Eff '[Stmt xsource i]

----------------------------------------------------------------------------------------------------
-- Functions to help with type inferences                                                         --
----------------------------------------------------------------------------------------------------

-- | Insert a blank line in the project output.
blankLine_ :: forall xsource i. () => Project xsource i ()
blankLine_ = blankLine__ @xsource @i

{- | Add a comment to the project.

Comments appear in generated code and help document the analysis.
-}
comment_ :: forall xsource i. () => Text -> Project xsource i ()
comment_ = comment__ @xsource @i

-- | Insert a divider line for visual separation.
divider_ :: forall xsource i. () => Project xsource i ()
divider_ = divider__ @xsource @i

{- | Estimate a statistical model.

Runs the provided estimator (regression, summary statistics, etc.) and stores
the results for later output with 'table_' or 'tableDisplay_'.

==== __Example__

@
estimate_ myRegression
@
-}
estimate_ :: forall xsource i a. (Estimator xsource i a) => a -> Project xsource i ()
estimate_ = estimate__ @xsource @i

{- | Output estimation results table.

Takes a list of model names and optionally saves the results to a file.
Typically used indirectly, with 'tableDisplay_' or 'tableSave_' calling
this function as necessary.
-}
table_ :: forall xsource i. () => Maybe FilePath -> [Text] -> Project xsource i ()
table_ = table__ @xsource @i

{- | Add a section header.

Headers help organize the project into logical sections.

==== __Example__

@
header_ "Main Results"
@
-}
header_ :: forall xsource i. () => Text -> Project xsource i ()
header_ = header__ @xsource @i

{- | Observe variable values for debugging.

Generates queries that display variable values, useful for verifying
data during development.

==== __Example__

@
observe_ [car, dealValue, leverage]
@
-}
observe_ :: forall xsource i. () => [SpecVar xsource i] -> Project xsource i ()
observe_ = mapM_ (observe__ @xsource @i)

{- | Load data from a CSV file.

Users generally don't call this directly; the transpiler inserts it when
necessary. For example, the Stata transpiler uses readCsv_ to import the data
for estimation that is generated by the SQL code.

==== __Example__

@
readCsv_ "deal_data.csv"
@
-}
readCsv_ :: forall xsource i. () => Text -> Project xsource i ()
readCsv_ = readCsv__ @xsource @i

{- | Insert raw code.

Allows embedding arbitrary commands that aren't otherwise supported
by the DSL.

==== __Example__

@
raw_ "gen log_assets = log(assets)"
@
-}
raw_ :: forall xsource i. () => Text -> Project xsource i ()
raw_ = raw__ @xsource @i


{- | Display estimation results table.

Runs a block of estimations and outputs their results to the console.

==== __Example__

@
tableDisplay_ $ do
    estimate_ baseline
    estimate_ withControls
    estimate_ fullModel
@
-}
tableDisplay_
    :: forall xsource i r a
     . ()
    => Eff (Stmt xsource i ': r) a
    -> Eff (Stmt xsource i ': r) ()
tableDisplay_ = tableSection Nothing

{- | Save estimation results table to a file.

Runs a block of estimations and saves their results to a text file.
The file name is derived from the title.

==== __Example__

@
tableSave_ "Table 1: Main Results" $ do
    estimate_ baseline
    estimate_ withControls
    estimate_ fullModel
@
-}
tableSave_
    :: forall xsource i r a
     . ()
    => FilePath
    -> Eff (Stmt xsource i ': r) a
    -> Eff (Stmt xsource i ': r) ()
tableSave_ title sem = do
    header__ @xsource @i $ T.pack title
    tableSection (Just fileName) sem
  where
    fileName = (<.> "txt") . T.unpack . T.toLower . T.replace " " "_" . T.pack $ title

{- | Internal helper for outputting estimation results table.

Runs a block of estimations and outputs their results using 'table__'.
This is the implementation behind 'tableDisplay_' and 'tableSave_'.

The function:

1. Extracts model names from the project using 'modelNames'
2. Executes the project (running all estimations)
3. Outputs the results with 'table__'

Used internally by 'tableDisplay_' (with @Nothing@ filepath) and
'tableSave_' (with @Just filepath@).
-}
tableSection
    :: forall xsource i r a
     . ()
    => Maybe FilePath
    -> Eff (Stmt xsource i ': r) a
    -> Eff (Stmt xsource i ': r) ()
tableSection mfp sem = do
    models <- raise $ modelNames sem
    sem
    table__ @xsource @i mfp models

